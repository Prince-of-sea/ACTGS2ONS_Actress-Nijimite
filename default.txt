;mode800
*define

caption "<<-TITLE->> for ONScripter"

rmenu "セーブ",save,"ロード",load,"リセット",reset
savenumber 18
transmode alpha
globalon
rubyon
saveon
nsa
humanz 10
windowback

;なにこれ面倒くせぇ
pretextgosub *pretext_lb
defsub errmsg
defsub vo
defsub msg2
defsub cls
defsub se_wait
defsub random
defsub select_set
defsub select_reset
defsub select_start
defsub wait_def
defsub bg_def
defsub sp_def
defsub sp_fo
defsub fo
defsub bgm_fo
defsub bgm_stop
defsub sleep

effect 10,10,200

;<<-EFFECT->>

game
;----------------------------------------
*errmsg
	csp -1:print 1
	bg black,1
	
	Ｃｏｎｖｅｒｔ　Ｅｒｒｏｒ！@
	このメッセージが見れるのはおかしいよ@
	クリックでタイトルに戻ります…\
return


*bgm_fo
	bgmfadeout 1000
	stop
	bgmfadeout 0
return


*sleep
	wait 1000
return


*bgm_stop
	stop
return

*vo
	getparam $0
	if $0=="" dwavestop 1
	if $0!="" dwave 1,"wav_dec\" + $0 + ".wav"
	mov $10,""
return


*msg2
	getparam $10
return


*pretext_lb
	lsp 10,":s/26,26,0;#ffffff"+$10,33,495-5-26 ;名前の表示
return


*cls
	csp -1
return


*se_wait
	;旧ONSでSE待機は再現不可なので
	wait 2000
return


*random
	getparam %0
	rnd %41,%0
return


*select_set
	getparam $0
	
	if $21=="" mov $21,$0:goto *ssend
	if $21!="" if $22=="" mov $22,$0:goto *ssend
	if $21!="" if $22!="" if $23=="" mov $23,$0:goto *ssend
	if $21!="" if $22!="" if $23!="" if $24=="" mov $24,$0:goto *ssend
	if $21!="" if $22!="" if $23!="" if $24!="" if $25=="" mov $25,$0:goto *ssend
	*ssend
return


*select_reset
	mov $21,""
	mov $22,""
	mov $23,""
	mov $24,""
	mov $25,""
return


*select_start
	setwindow 60,60,27,6,26,26,0,50,20,0,0,#999999,50,50,749,549;ウィンドウselect用
	
	if $21=="NULL" mov $21,""
	if $22=="NULL" mov $22,""
	if $23=="NULL" mov $23,""
	if $24=="NULL" mov $24,""
	if $25=="NULL" mov $25,""
	
	if $21=="" if $22=="" if $23=="" if $24=="" if $25=="" goto *selskip
	select $21,*S1, $22,*S2, $23,*S3, $24,*S4, $25,*S5
	
	*S1
		mov $21,"":mov %40,1:goto *selskip
	*S2
		mov $22,"":mov %40,2:goto *selskip
	*S3
		mov $23,"":mov %40,3:goto *selskip
	*S4
		mov $24,"":mov %40,4:goto *selskip
	*S5
		mov $25,"":mov %40,5:goto *selskip
	
	*selskip
	setwindow 33,495,27,3,26,26,0,5,20,0,0,#999999,0,455,799,599;ウィンドウ汎用
return


*wait_def
	getparam %0
	wait %0*100
return


*bg_def
	getparam $0,%0,$1,$2,$3
	vsp 15,0:vsp 16,0:vsp 17,0
	
	if %0==2 lsp 15,"cg\"+$2+".png",   0,0
	if %0==3 lsp 16,"cg\"+$2+".png", 180,0
	if %0==3 lsp 17,"cg\"+$2+".png",-180,0
	
	
	if $1=="" goto *nobg
	
	if %0!=0 if $0=="fi" bg "cg\"+$1+".png",10
	if %0!=0 if $0==""   bg "cg\"+$1+".png",1
	if %0==0 if $2=="FADE_SET" bg "cg\"+$1+".png",10
	if %0==0 if $2!="FADE_SET" bg "cg\"+$1+".png",1
return
	*nobg
	
	if %0!=0 if $0=="fi" bg black,10
	if %0!=0 if $0==""   bg black,1
	if %0==0 if $2=="FADE_SET" bg black,10
	if %0==0 if $2!="FADE_SET" bg black,1
return


*sp_def
	getparam $0,%0,$1,$2,$3
	vsp 15,0:vsp 16,0:vsp 17,0
	if $0=="fi" print 10
	
	if %0==1 lsp 15,"cg\"+$1+".png",   0,0
	if %0==2 lsp 16,"cg\"+$1+".png", 180,0:lsp 17,"cg\"+$2+".png",-180,0
	if %0==3 lsp 15,"cg\"+$1+".png",   0,0:lsp 16,"cg\"+$2+".png", 180,0:lsp 17,"cg\"+$3+".png",-180,0
	
	print 10
return


*sp_fo
	csp 0
	csp 1
	csp 2
	csp 3
	csp 4
	csp 5
	csp 6
	csp 7
	csp 8
	csp 9
	print 10
return


*fo
	vsp 15,0:vsp 16,0:vsp 17,0
	bg black,10
return

;----------------------------------------
;[memo]
;数字変数
;	%0~%9 getparam汎用
;	%10 疑似ACTGS括弧再現用
;	%11 直近のselectが2か否か
;	%20 タイトル用btnwait
;	%21~%25 select用疑似配列(?)
;	
;	%40 S = 直前のセレクト 
;	%41 R = 直前のランダム 
;	%42 K = 直前の？？？？ - [KAISOU]	シーン回想用フラグかな(未確認)
;	%43 T = 直前の？？？？ - [TRIAL]	体験版かな(bg1_fi "_スペック" とかあるし)
;	%44 L = 直前の？？？？ - [LABEL]	シーン回想時の番号っぽい(1-5)
;	
;	%50~%199 ACTGS自動割り当て(通常)
;	%200~%?? ACTGS自動割り当て(グローバル)
;	
;文字変数
;	$0~$9 getparam汎用
;	$10 キャラ名前
;	
;スプライト
;	0~9 ACTGSスプライト
;	10 名前
;	11 _white
;	12 black
;	
;	15 立ち絵前
;	16 立ち絵右
;	17 立ち絵左
;	20~タイトル
;----------------------------------------
*start
bgmvol   <<-BGMVOL->>		;BGM音量
voicevol <<-VOICEVOL->>		;ボイス音量
defsevol <<-SEVOL->>		;効果音音量

setwindow 33,495,27,3,26,26,0,5,20,0,0,#999999,0,455,799,599;ウィンドウ汎用

goto *SCR_open
;----------------------------------------
*niji_title_menu

bgm "wav_dec\BGM01.wav"

if %1000!=1 lsp 20,"cg\_MnChara.png",0,0
if %1000==1 lsp 20,"cg\_MnChara.png",0,0
if %1000!=1 bg "cg\_MnBack.png",1
if %1000==1 bg "cg\_MnBack5.png",1

lsp 30,"cg\_MnStart.png"  ,75, 84
lsp 31,"cg\_MnCont.png"   ,75,148
lsp 32,"cg\_MnOmake.png"  ,75,212
lsp 33,"cg\_MnOption.png" ,75,276
lsp 34,"cg\_MnEnd.png"    ,75,340
lsp 35,"cg\_MnStart.png"  ,75, 84,128
lsp 36,"cg\_MnCont.png"   ,75,148,128
lsp 37,"cg\_MnOmake.png"  ,75,212,128
lsp 38,"cg\_MnOption.png" ,75,276,128
lsp 39,"cg\_MnEnd.png"    ,75,340,128


print 1
*niji_title_loop
	bclear
	btrans
	
	exbtn_d "C30C31C32C33C34"
	exbtn 30,30,"P30C31C32C33C34"
	exbtn 31,31,"C30P31C32C33C34"
	exbtn 32,32,"C30C31P32C33C34"
	exbtn 33,33,"C30C31C32P33C34"
	exbtn 34,34,"C30C31C32C33P34"
	
	print 1
	btnwait %20
	if %20==30 csp -1:stop:goto *niji_title_menu_end
	if %20==31 csp -1:stop:bg "cg\_MLLoadBack.png",10:systemcall load:bg black,10:goto *niji_title_menu
	if %20==32 csp -1:stop:bg black,10:mpegplay "NijiOp.mpg",1:goto *niji_title_menu
	if %20==34 csp -1:stop:end
	
goto *niji_title_loop
;----------------------------------------
