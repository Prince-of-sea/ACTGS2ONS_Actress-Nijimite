;mode800
*define

caption "<<-TITLE->> for ONScripter"

rmenu "セーブ",save,"ロード",load,"ログ",lookback,"リセット",reset
savename "ＳＡＶＥ","ＬＯＡＤ","ＤＡＴＡ"
savenumber 18
transmode alpha
globalon
rubyon
saveon
nsa
humanz 10
windowback

;なにこれ面倒くせぇ
pretextgosub *pretext_lb
defsub errmsg
defsub vo
defsub msg2
defsub cls
defsub se_wait
defsub random
defsub select_set
defsub select_reset
defsub select_start
defsub wait_def
defsub bg_def
defsub sp_def
defsub sp_fo
defsub fo
defsub bgm_fo
defsub bgm_stop
defsub sleep

effect 10,10,200

;<<-EFFECT->>

game
;----------------------------------------
*errmsg
	csp -1:print 1
	bg black,1
	
	Ｃｏｎｖｅｒｔ　Ｅｒｒｏｒ！@
	このメッセージが見れるのはおかしいよ@
	クリックでタイトルに戻ります…\
return


*bgm_fo
	bgmfadeout 1000
	stop
	bgmfadeout 0
return


*sleep
	wait 1000
return


*bgm_stop
	stop
return

*vo
	getparam $0
	if $0=="" dwavestop 0
	if $0!="" dwave 0,"wav_dec\" + $0 + ".wav"
	mov $10,""
return


*msg2
	getparam $10
return


*pretext_lb
	lsp 10,":s/26,26,0;#ffffff"+$10,33,495-5-26 ;名前の表示
return


*cls
	csp -1
return


*se_wait
	;旧ONSでSE待機は再現不可なので
	wait 2000
return


*random
	getparam %0
	rnd %41,%0
return


*select_set
	getparam $0
	
	if $21=="" mov $21,$0:goto *ssend
	if $21!="" if $22=="" mov $22,$0:goto *ssend
	if $21!="" if $22!="" if $23=="" mov $23,$0:goto *ssend
	if $21!="" if $22!="" if $23!="" if $24=="" mov $24,$0:goto *ssend
	if $21!="" if $22!="" if $23!="" if $24!="" if $25=="" mov $25,$0:goto *ssend
	*ssend
return


*select_reset
	mov $21,""
	mov $22,""
	mov $23,""
	mov $24,""
	mov $25,""
return


*select_start
	vsp 10,0:setwindow 60,60,27,6,26,26,0,50,20,0,0,#999999,50,50,749,549;ウィンドウselect用
	
	if $21=="NULL" mov $21,""
	if $22=="NULL" mov $22,""
	if $23=="NULL" mov $23,""
	if $24=="NULL" mov $24,""
	if $25=="NULL" mov $25,""
	
	if $21=="" if $22=="" if $23=="" if $24=="" if $25=="" goto *selskip
	select $21,*S1, $22,*S2, $23,*S3, $24,*S4, $25,*S5
	
	*S1
		mov $21,"":mov %40,1:goto *selskip
	*S2
		mov $22,"":mov %40,2:goto *selskip
	*S3
		mov $23,"":mov %40,3:goto *selskip
	*S4
		mov $24,"":mov %40,4:goto *selskip
	*S5
		mov $25,"":mov %40,5:goto *selskip
	
	*selskip
	setwindow 33,495,29,3,26,26,0,5,20,0,0,#999999,0,455,799,599;ウィンドウ汎用
return


*wait_def
	getparam %0
	wait %0*1000
return


*bg_def
	getparam $0,%0,$1,$2,$3
	vsp 15,0:vsp 16,0:vsp 17,0:vsp 10,0
	
	if %0==2 lsp 15,"cg\"+$2+".png",   0,0
	if %0==3 lsp 16,"cg\"+$2+".png",-180,0
	if %0==3 lsp 17,"cg\"+$3+".png", 180,0
	
	
	if $1=="" goto *nobg
	
	if %0!=0 if $0=="fi" bg "cg\"+$1+".png",10
	if %0!=0 if $0==""   bg "cg\"+$1+".png",1
	if %0==0 if $2=="FADE_SET" bg "cg\"+$1+".png",10
	if %0==0 if $2!="FADE_SET" bg "cg\"+$1+".png",1
return
	*nobg
	
	if %0!=0 if $0=="fi" bg black,10
	if %0!=0 if $0==""   bg black,1
	if %0==0 if $2=="FADE_SET" bg black,10
	if %0==0 if $2!="FADE_SET" bg black,1
return


*sp_def
	getparam $0,%0,$1,$2,$3
	vsp 15,0:vsp 16,0:vsp 17,0
	if $0=="fi" print 10
	
	if %0==1 lsp 15,"cg\"+$1+".png",   0,0
	if %0==2 lsp 16,"cg\"+$1+".png",-180,0:lsp 17,"cg\"+$2+".png", 180,0
	if %0==3 lsp 15,"cg\"+$1+".png",   0,0:lsp 16,"cg\"+$2+".png",-180,0:lsp 17,"cg\"+$3+".png", 180,0
	
	print 10
return


*sp_fo
	csp 0:csp 1:csp 2:csp 3:csp 4:csp 5:csp 6:csp 7:csp 8:csp 9
	print 10
return


*fo
	vsp 15,0:vsp 16,0:vsp 17,0
	bg black,10
return

;----------------------------------------
;[memo]
;数字変数
;	%0~%9 getparam汎用
;	%10 疑似ACTGS括弧再現用
;	%11 直近のselectが2か否か
;	%20 タイトル用btnwait
;	%21~%25 select用疑似配列(?)
;	
;	%40 S = 直前のセレクト 
;	%41 R = 直前のランダム 
;	%42 K = 直前の？？？？ - [KAISOU]	シーン回想用フラグかな(未確認)
;	%43 T = 直前の？？？？ - [TRIAL]	体験版かな(bg1_fi "_スペック" とかあるし)
;	%44 L = 直前の？？？？ - [LABEL]	シーン回想時の番号っぽい(1-5)
;	
;	%50~%199 ACTGS自動割り当て(通常)
;	%200~%?? ACTGS自動割り当て(グローバル)
;	
;文字変数
;	$0~$9 getparam汎用
;	$10 キャラ名前
;	
;スプライト
;	0~9 ACTGSスプライト
;	10 名前
;	11 _white
;	12 black
;	
;	15 立ち絵前
;	16 立ち絵右
;	17 立ち絵左
;	20~タイトル
;----------------------------------------
*start
setwindow 33,495,29,3,26,26,0,5,20,0,0,#999999,0,455,799,599;ウィンドウ汎用

goto *SCR_open
;----------------------------------------
*volmenu_GUI
	;https://gist.github.com/Prince-of-sea/325b8ae6912ecf23316a71c3d008480c
	;文字/数字/スプライト/ボタン
	;全部130~149までを使ってます - 競合に注意
	
	;バー文字列定義
	mov $130,":s;#FFFFFF#666666○――――――――――"
	mov $131,":s;#FFFFFF#666666―○―――――――――"
	mov $132,":s;#FFFFFF#666666――○――――――――"
	mov $133,":s;#FFFFFF#666666―――○―――――――"
	mov $134,":s;#FFFFFF#666666――――○――――――"
	mov $135,":s;#FFFFFF#666666―――――○―――――"
	mov $136,":s;#FFFFFF#666666――――――○――――"
	mov $137,":s;#FFFFFF#666666―――――――○―――"
	mov $138,":s;#FFFFFF#666666――――――――○――"
	mov $139,":s;#FFFFFF#666666―――――――――○―"
	mov $140,":s;#FFFFFF#666666――――――――――○"
	
*volmenu_loop
	;取得
	getbgmvol   %130
	getsevol    %131
	getvoicevol %132
	
	;文字列変換
	itoa2 $141,%130
	itoa2 $142,%131
	itoa2 $143,%132
	
	;バー代入
	if %130==  0 mov $146,$130
	if %130== 10 mov $146,$131
	if %130== 20 mov $146,$132
	if %130== 30 mov $146,$133
	if %130== 40 mov $146,$134
	if %130== 50 mov $146,$135
	if %130== 60 mov $146,$136
	if %130== 70 mov $146,$137
	if %130== 80 mov $146,$138
	if %130== 90 mov $146,$139
	if %130==100 mov $146,$140
	if %131==  0 mov $147,$130
	if %131== 10 mov $147,$131
	if %131== 20 mov $147,$132
	if %131== 30 mov $147,$133
	if %131== 40 mov $147,$134
	if %131== 50 mov $147,$135
	if %131== 60 mov $147,$136
	if %131== 70 mov $147,$137
	if %131== 80 mov $147,$138
	if %131== 90 mov $147,$139
	if %131==100 mov $147,$140
	if %132==  0 mov $148,$130
	if %132== 10 mov $148,$131
	if %132== 20 mov $148,$132
	if %132== 30 mov $148,$133
	if %132== 40 mov $148,$134
	if %132== 50 mov $148,$135
	if %132== 60 mov $148,$136
	if %132== 70 mov $148,$137
	if %132== 80 mov $148,$138
	if %132== 90 mov $148,$139
	if %132==100 mov $148,$140
	
	;画面作成
	lsp 130,":s;#FFFFFF［Ｃｏｎｆｉｇ］", 50, 50
	lsp 131,":s;#FFFFFF#666666リセット", 400,450
	lsp 132,":s;#FFFFFF#666666戻る",     550,450
	
	lsp 135,":s;#FFFFFFＢＧＭ",           50,150
	lsp 136,":s;#FFFFFF#666666＜",       200,150
	lsp 137,$146,                        250,150
	lsp 138,":s;#FFFFFF#666666＞",       550,150
	lsp 139,":s;#FFFFFF#666666"+$141,    600,150
	
	lsp 140,":s;#FFFFFFＳＥ",             50,250
	lsp 141,":s;#FFFFFF#666666＜",       200,250
	lsp 142,$147,                        250,250
	lsp 143,":s;#FFFFFF#666666＞",       550,250
	lsp 144,":s;#FFFFFF#666666"+$142,    600,250
	
	lsp 145,":s;#FFFFFFＶＯＩＣＥ",       50,350
	lsp 146,":s;#FFFFFF#666666＜",       200,350
	lsp 147,$148,                        250,350
	lsp 148,":s;#FFFFFF#666666＞",       550,350
	lsp 149,":s;#FFFFFF#666666"+$143,    600,350
	
	print 1
	
	;ボタン定義
	bclear
	spbtn 131,131
	spbtn 132,132
	spbtn 136,136
	spbtn 138,138
	spbtn 141,141
	spbtn 143,143
	spbtn 146,146
	spbtn 148,148
	
	;入力待ち
	btnwait %140
	
	if %140==131 bgmvol 100:sevol 100:voicevol 100
	if %140==132 csp -1:reset
	if %140==136 if %130!=  0 sub %130,10:bgmvol %130
	if %140==138 if %130!=100 add %130,10:bgmvol %130
	if %140==141 if %131!=  0 sub %131,10:sevol %131
	if %140==143 if %131!=100 add %131,10:sevol %131
	if %140==146 if %132!=  0 sub %132,10:voicevol %132
	if %140==148 if %132!=100 add %132,10:voicevol %132
	
goto *volmenu_loop
;----------------------------------------
*niji_title_menu

bgm "wav_dec\BGM01.wav"

if %1000!=1 lsp 20,"cg\_MnChara.png",0,0
if %1000==1 lsp 20,"cg\_MnChara.png",0,0
if %1000!=1 bg "cg\_MnBack.png",1
if %1000==1 bg "cg\_MnBack5.png",1

lsp 30,"cg\_MnStart.png"  ,75, 84
lsp 31,"cg\_MnCont.png"   ,75,148
lsp 32,"cg\_MnOmake.png"  ,75,212
lsp 33,"cg\_MnOption.png" ,75,276
lsp 34,"cg\_MnEnd.png"    ,75,340
lsp 35,"cg\_MnStart.png"  ,75, 84,128
lsp 36,"cg\_MnCont.png"   ,75,148,128
lsp 37,"cg\_MnOmake.png"  ,75,212,128
lsp 38,"cg\_MnOption.png" ,75,276,128
lsp 39,"cg\_MnEnd.png"    ,75,340,128


print 1
*niji_title_loop
	bclear
	btrans
	
	exbtn_d "C30C31C32C33C34"
	exbtn 30,30,"P30C31C32C33C34"
	exbtn 31,31,"C30P31C32C33C34"
	exbtn 32,32,"C30C31P32C33C34"
	exbtn 33,33,"C30C31C32P33C34"
	exbtn 34,34,"C30C31C32C33P34"
	
	print 1
	btnwait %20
	if %20==30 csp -1:stop:goto *niji_title_menu_end
	if %20==31 csp -1:stop:bg "cg\_MLLoadBack.png",10:systemcall load:bg black,10:goto *niji_title_menu
	if %20==32 csp -1:stop:bg black,10:mpegplay "NijiOp.mpg",1:goto *niji_title_menu
	if %20==33 csp -1:stop:bg black,10:goto *volmenu_GUI
	if %20==34 csp -1:stop:end
	
goto *niji_title_loop
;----------------------------------------
